plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version '2.1.20'
    id "edu.wpi.first.GradleRIO" version "2025.3.2"
}


sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def ROBOT_MAIN_CLASS = "frc.robot.Main"

// ------------------------------------------------------------------
// verify that deploy/music contains exactly 0.chrp through 7.chrp
// ------------------------------------------------------------------
def verifyMusicFiles = tasks.register("verifyMusicFiles") {
    description = "Ensure all required .chrp music files are present"
    doLast {
        def missing = []
        (0..7).each { idx ->
            def f = file("src/main/deploy/music/${idx}.chrp")
            if (!f.exists()) {
                missing << f.name
            }
        }
        if (!missing.isEmpty()) {
            throw new GradleException("Missing music files under 'src/main/deploy/music/': ${ missing.join(', ') }")
        }
    }
}

// Hook it into the standard 'check' lifecycle
tasks.named("check") {
    dependsOn verifyMusicFiles
}

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project DeployUtils.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                }
            }
        }
    }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for JNI.
wpi.java.debugJni = false

// Set this to true to enable desktop support.
def includeDesktopSupport = false

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 5.
dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    implementation "com.ctre.phoenix6:wpiapi-java:25.3.2"
    implementation "com.ctre.phoenix:api-java:5.35.0"
    implementation "com.ctre.phoenix:wpiapi-java:5.35.0"
    implementation "com.revrobotics.frc:REVLib-java:2025.0.3"

    implementation files("libs/teaforge-0.1.3.jar", "libs/teaforge-roborio-0.1.4.jar")

}

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

// Simulation configuration (e.g. environment variables).
wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Configure jar and deploy tasks
deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

// Configure string concat to always inline compile
tasks.withType(JavaCompile) {
    options.compilerArgs.add '-XDstringConcat=inline'
}
repositories {
    mavenCentral()

    maven { url "https://maven.ctr-electronics.com/release/" }
    maven { url "https://maven.revrobotics.com/" }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = targetCompatibility
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = targetCompatibility
    }
}

wrapper {
    gradleVersion = '8.8'
    distributionType = Wrapper.DistributionType.BIN
}